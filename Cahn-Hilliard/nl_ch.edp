/* Program to solve the Cahn Hilliard (Non-Linear) Equation subject to 
	dirichlet boundary condtions
	
	u_t - \Laplacian^2 u + u^3 = f in \Omega
	
	u(x,y,0) = sin(pi*x)*sin(pi*y)
	\Laplacian u = g_1 on \Partial \Omega
	u = g2 on \Partial \Omega
	
	Exact u(x,y,t) = exp(-t)*sin(pi*x)*sin(pi*y)
	f(x,y) = (-4*pi^4-1)*exp(-t)*sin(pi*x)*sin(pi*y) + (exp(-t)*sin(pi*x)*sin(pi*y))^3

*/
verbosity = 0;

bool debug = true;
int N = 30;

mesh Th = square(N,N);
fespace Vh(Th,P1); //Using P1 elements

Vh uh,ph,phi,psi;

func real f(real t) {
	return (-4*pi^4-1)*exp(-t)*sin(pi*x)*sin(pi*y) + (exp(-t)*sin(pi*x)*sin(pi*y))^3;
}
Vh uh0 = sin(pi*x)*sin(pi*y);

Vh uu = uh0^2;
Vh u0 = uh0; 

Vh f1;

real T = 3.0, delt = 0.1, t;

macro Grad(u) [dx(u), dy(u)]//

//Problem - Coupled System
problem CH([uh,ph],[phi,psi]) = int2d(Th)(uh*phi + delt*Grad(ph)'*Grad(phi) - Grad(uh)'*Grad(psi) - ph*psi + uu*uh*phi) + 
								int2d(Th)(-delt*f1*phi - u0*phi) + on(1,2,3,4,uh=0,ph=0);

for (int i=0; i<T/delt; i++)
{
	t = (i+1)*delt;
	f1 = f(t);
	real error = 1000, tol = 1e-14;
	while (error > tol)
	{
		CH;
		uu = uh^2;
		error = sqrt(int2d(Th)(uh-u0)^2);
		u0 = uh;
	}
	uh0 = uh;
	//plot(uh,wait=debug,value=true);
}

Vh ue = exp(-t)*sin(pi*x)*sin(pi*y);

plot(uh,wait=debug,value=debug);
plot(ue,wait=debug,value=debug);

real l2error = int2d(Th)(uh-ue)^2;

cout<<"L2error = "<<sqrt(l2error)<<endl;